#-----------------------------------------------------------------------------
# This macro creates QLogic dumps for port arg0
#
define QLD
  set $PORT = $arg0
  if ispdump[$PORT].addr != 0
    dump binary memory $arg1 ispdump[$PORT].addr (ispdump[$PORT].addr+ispdump[$PORT].length)
  else
    printf "[NOCORE] NO QLOGIC CORE PRESENT PORT %-2.2x  \n",$PORT
  end
end

define QueDmp
  set $PORT = $arg0
  set $que_f = (unsigned long)ispstr[$PORT]->$arg1->begin
  set $que_l = (unsigned long)ispstr[$PORT]->$arg1->end
  if $que_f != $que_l
    dump binary memory $arg2 $que_f $que_l
  end
end
#----

define        QLDump
  QLD $arg0 qldmp$arg1$arg0.bin
  QueDmp $arg0 reqQue qldmpreq$arg1$arg0.bin
  QueDmp $arg0 resQue qldmpres$arg1$arg0.bin
  QueDmp $arg0 atioQue qldmpatio$arg1$arg0.bin
  set $que_f = (unsigned long)asyqa[$arg0]->begin
  set $que_l = (unsigned long)asyqa[$arg0]->end
  dump binary memory qldmpasyqa$arg1$arg0.bin $que_f $que_l
end
#---
document QLDump
Macro "QLDump <port> <proc>" takes a QLogic dump.
end

