*

* If tempo is "x,4" the total number of measures to put

* in the loop is: (x)^*(desired # of minutes)-16

* (or else (x)^*(desired # of minutes)-12 if all fading out

* in a whole note length measure after loop...)

*

* rnd(8) is a 8^*1 randum number vector

* endcord(8) contains the note numbers of the endcord chord to

* glide to (optional use)

* prev(8) contains the previous notes being played

*

vars    rnd(8),endcord(8),prev(8)

vars    loopnum,count

vars    i,j,select,numstaves,endchord

*

tempo   9,4

format  systems

calc    loopnum ^= 9

calc    numstaves ^= 8

calc    endchord ^= -1

*

title   The Invasion of the

title   Entroid Sensory Units

instr   musique,Entroid

*

staff   1:      1

staff   2:      2

staff   3:      3

staff   4:      4

staff   5:      5

staff   6:      6

staff   7:      7

staff   8:      8

*

macro   entroid

*

* select one staff to vary the pitch on

*

randu   select,numstaves

*

* select a new pitch value

*

randu   rnd(select),65

calc    rnd(select) ^= rnd(select) + 5

*

* decrement even-numbered staves and increment odd-numbered

* staves' pitch by a little

*

loop    i ^= 1,numstaves

.       calcs   [(i/2)=int(i/2)],rnd(i)^=rnd(i)-.4,rnd(i)+.6

.       STFS{i,1}:      (rnd(i),.25)

endloop

*

endmacro

*

vars    STFS

encode  STFS ^=1 2 3 4 5 6 7 8

*

* %%%%%%%%%%%%% Here is where things start! %%%%%%%%%%%%%%

*

loop    i ^= 1,numstaves

*

* initialization

*

.       STFS{i,1}:      glide   9

*

* make initial notes lower-pitched (less annoying)

*

.       randu   rnd(i),50

.       calc    rnd(i) ^= rnd(i) + 5

*

.       if      endchord

.       .       calc    endcord(i) ^= 4.7^*i + 10.2

.       endif

*

* bring 1 staff in per measure for 1^us^ut "numstaves" measures

*

.       measure (i)

.       STFS{i,1}:    tie vpp<

.       loop    j ^= i,1,-1

.       .       STFS{j,1}:    (rnd(j),.25)

.       endloop

endloop

*

* generate the actual measures here

*

loop    count ^= 1,loopnum

.       measure (numstaves + count)

.       entroid

endloop

*

* do the appropriate ending

*

if      endchord                $$ glide to ending chord

.       measure (numstaves + loopnum + 1)

*

.       loop    i ^= 1,numstaves

.       .       STFS{i,1}:    vff> (rnd(i))@D(endcord(i),1.25) v17

.       endloop

*

else            $$ slow fade out

*

.       loop    i ^= 1,numstaves

.       .       STFS{i,1}:    vff>

.       endloop

*

.       loop    i ^= numstaves,1,-1

.       .       measure (numstaves+loopnum-i+numstaves+1)

.       .       loop    j ^= 1,i

.       .       .       STFS{j,1}:    (rnd(j),.25)

.       .       endloop

.       STFS{i,1}:    v17

.       endloop

*

endif
