*

* This file contains routines that make music

* printing more uniform.  Included are:

*

* macro mytitle         (generates title commands)

*

* macro fingers         (generates fingerings)

*

* macro invtitl         (inverted keyboard title)

*

* process eightva       (generates 8^^^uva-------^^^d| markings)

*

* Some miscellaneous commands:

*

format  display off

format  page numbering off

format  systems

*

************************************************************

*

macro   mytitle TITLE,COMPOSER

*

* Pass the title and composer of a piece and this

* will generate the title commands (useful when

* printing many pieces; all titles will be formatted

* the same way).

*

title   ^*/g3TITLE

title

title   /g1COMPOSER

endmacro

*

************************************************************

*

* These macros provides a shorthand way of entering

* fingering notations.

*

* Macros i,j,k are used for fingerings above the staff

* line; Macros m,n,o are used for fingerings below the

* staff line.  Macros i,m take 1 argument; Macros j,n take

* 2 arguments; Macros k,o take 3 arguments.

*

* To finger a single note type e.g. "i 4."   (no quotes)

* To finger a 2-note chord type e.g. "j 2 5."

* To finger a 3-note chord type e.g. "k 1 3 5."

*

* The first argument is always the lowest plotted number,

* followed by the second argument above it, etc...

*

* Set -format lyrics offset- to 0 to make the numbers

* plot next to the notes (need to put Shift-SUPER

* and Shift-SUB in other lyrics [remove volumes and

* enter them as lyrics by hand]).

*

macro   i A.

        "@W^//s2A"

endmacro

*

macro   j A B.

        i A.

        i B.

endmacro

*

macro   k A B C.

        j A B.

        i C.

endmacro

*

* these macros are for fingerings below the staff

*

macro   m A.

        "@X^//s2A"

endmacro

*

macro   n A B.

        m B.

        m A.

endmacro

*

macro   o A B C.

        m C.

        n A B.

endmacro

*

macro   invtitl

*

* Call this when printing an inverted-keyboard version

* of a piece.

*

title   (Inverted keyboard process by Tom Cortese)

endmacro

*

************************************************************

*

* This process will perform an 8va on notes starting

* after an ON character and ending with an OFF character.

* 8^uv^ua is plotted with a variable number of dashes following

* it. Type ON15 on an OPAL line to plot 15 dashes; notes

* are transcribed down 1 octave until OFF is reached.

* Format -lyrics offset- may also be useful (30 seems ok)

*

vars    i

*

process eightva,A

*

.       encode  NUM ^=A;after,ON

*

* if NUM has a non-nil value, this is a turn-on marker,

* and NUM contains the number of dashes to plot.  If NUM

* has no value, it is either a turn-off marker or an

* OPAL object.

*

.       if      "NUM" @= ""

..      transpose       +12

..      transcribe down 1 octave

*

* output the lyric with the variable number of dashes

*

.       encode  LYR ^="@W^^^d8^^^uva

*

.       loop    i ^= 1,NUM

..      encode  LYR ^=LYR-

.       endloop

*

.       encode  LYR ^=LYR^^^d|^^^u"

.       LYR

*

.       elseif  "A" = "OFF"

..      transpose       0

..      transcribe up 0 octaves

*

.       else

..      A

.       endif

*

endprocess

*

* LYR is used for the 8^uv^ua lyric.  ON and OFF store the

* characters that turn the process on and off.  NUM is

* used to tell how many dashes to plot.

*

vars    LYR,ON,OFF,NUM

encode  ON ^=%

encode  OFF ^=_
