*-- include piano,standard print stuff
#include "standard-print-stuff.include"

staff   one: 1, 2

staff   two: 3, 4

one:    treble key d minor

two:    bass key d minor

time    3,8

tempo   130, 8

instr   musique,Organ

format  systems 1,8,16,24,31,38,45;

format  page size 8,10.5

format  lyrics offset 0

mytitle Invention #4,by Johann Sebastian Bach

*

*-- include piano,process invert
#include "process-invert.include"

calc    inverting ^= -1

*

* Make character string variables to make different

* fingering macro calls depending on whether inverting

* or not (fingerings above/below notes get reversed)

*

vars    UP,DN

encode  UP ^=i

encode  DN ^=m

*

measure 1

if      inverting

.       invtitl

.       encode  UP ^=m

.       encode  DN ^=i

.       one:    bass key g major

.       two:    treble key g major

.       one:    begin invert

.       two:    begin invert

.       print   two,one

endif

*

one:    DN 2. 4d16 DN 1. e f g a b

two:    r4d

*

measure 2

one:    DN 1. 4c+16 b a g f e

two:    r4d

*

measure 3

one:    4f8 a 5d

two:    UP 5. 3d16 e f g a b

*

measure 4

one:    4g8 5c+ e

two:    UP 5. 3c+16 UP 2. b a g f e

*

measure 5

one:    UP 2. 5d16 UP 1. e f g a b

two:    DN 3. 3f8 a 4d

*

measure 6

one:    UP 1. 5c+16 b a g f e

two:    DN 4. 3e8 g 4c+

*

measure 7

one:    UP 3. 5f16 UP 1. d e f g a

two:    DN 5. 3d8 4d DN 4. 3f

*

measure 8

one:    UP 1. 4b16 5a g f e d

two:    3g8 a b

*

measure 9

one:    UP 3. 5e16 c d e f g

two:    3c8 4c DN 4. 3e

*

measure 10

one:    4a16 5g f e d c

two:    3f8 g a

*

measure 11

one:    UP 3. 5d16 UP 4. e UP 5. f d e f

two:    DN 3. 3b16 g a b 4c d

*

measure 12

one:    4g8 r r

two:    3e16 4d c 3b a g

*

measure 13

one:    5c16 d e c d e

two:    DN 3. 3a16 f g a b 4c

*

measure 14

one:    4f8 r UP 4. bt

two:    3d16 4c 3b a DN 1. g f

*

measure 15

one:    4b8 a g

two:    UP 3. 3e16 c d e f g

*

measure 16

one:    UP 5. 5c16 4b a g f e

two:    2a16 3g f e UP 1. d c

*

measure 17

one:    4f16 g g8d f16

two:    3d16 2b 3c8 2c

*

measure 18

one:    4f8 5c c

two:    DN 3. 2f16 g a DN 3. b 3c d

*

measure 19

one:    5c4dt

two:    2e16 3d c 2b a DN 5. g

*

measure 20

one:    5c4dt

two:    2a16 b 3c d DN 2. e f

*

measure 21

one:    5c4dt

two:    2g16 3f e d DN 1. c 2b

*

measure 22

one:    5c16 DN 4. 4b a g f DN 2. e

two:    2a16 b 3c 2a b 3c

*

measure 23

one:    5c16 4d e f+ DN 1. g a

two:    2f+8 r r

*

measure 24

one:    DN 3. 4b16 a g DN 3. f e d

two:    DN 4. 2g16 a b g a b

*

measure 25

one:    4b16 c d e DN 1. f g

two:    2e8 r r

*

measure 26

one:    4a16 UP 1. bn 5c d e UP 5. f

two:    2f8 DN 1. 3f DN 2. d

*

measure 27

one:    4g+16 5f e d c 4bn

two:    DN 3. 2bn8 DN 4. g+ DN 5. e

*

measure 28

one:    UP 3. 5c16 4bn 5d c 4b a

two:    DN 3. 2a16 g+ a bn 3c DN 3. d

*

measure 29

one:    DN 4. 4g+16 a g f+ e d

two:    3e4dt

*

measure 30

one:    DN 2. 4c16 DN 1. d e f+ g+ a

two:    3e4dt

*

measure 31

one:    4d16 UP 5. 5c UP 4. 4bn UP 3. a UP 2. g+

        UP 1. f+

two:    3e4dt

*

measure 32

one:    UP 2. 4e16 UP 1. f+ g+ a bn 5c

two:    3e4dt

*

measure 33

one:    UP 1. 4f+16 5e d c 4bn a

two:    3e4dt 

*

measure 34

one:    UP 2. 4g+16 UP 1. a bn 5c d e

two:    3e8 DN 1. 4e DN 2. d

*

measure 35

one:    4a16 5f e d c 4bn

two:    DN 3. 4c8 DN 4. 3bn DN 5. a

*

measure 36

one:    5a16 g+ f+ e a8t

two:    4d8 e f

*

measure 37

one:    5a16 d UP 3. 4bn8d a16

two:    4d8 e 3e

*

measure 38

one:    4a8d UP 3. a16 b 5c

two:    3a16 2a b 3c d e-

*

measure 39

one:    4d8 f+ a

two:    2f+16 DN 2. 3e- DN 1. d c 2b a

*

measure 40

one:    UP 3. 4b16 g a b 5c d

two:    2g8d DN 3. g16 a b

*

measure 41

one:    4e16 5d c 4b a g

two:    2c8 g 3c

*

measure 42

one:    4a8 5f16 e f8

two:    DN 4. 3f16 g a bn DN 2. 4c+ d

*

measure 43

one:    4g8 5e r

two:    3e16 DN 1. 4d c+ 3bn DN 1. a g

*

measure 44

one:    UP 2. 5d16 UP 1. e f g a b

two:    DN 3. 3f8 a 4d

*

measure 45

one:    5c+16 b a g f e

two:    DN 4. 3e8 g 4c+

*

measure 46

one:    5f8 d 4gt

two:    DN 5. 3d16 e f g a DN 2. b

*

measure 47

one:    4g16 UP 4. 5d c+ e 4a 5c

two:    3c+16 DN 2. b a g f e

*

measure 48

one:    5d16 4bn 5c+8d d16

two:    DN 3. 3f16 g a8 2a

*

measure 49

one:    5d16 c 4b a g f

two:    2b8d 3c16 2b a

*

measure 50

one:    DN 5. 4b16 DN 2. c+ DN 1. d e f g

two:    2g16 DN 2. 3b DN 1. a g f e

*

measure 51

one:    4a16 5d 4f8 e16 d

two:    3f16 g a8 2a

*

measure 52

one:    fermata 4d4d

two:    fermata 2d4d
