*
* Simulates 24-hour clock chimes
*
format  systems
vars    hour,ring,start,m
*
* hour: used to determine how many times to ring
* ring: number of times rung so far (looping variable)
* start: the initial hour when piece first played
* m: current measure number
*
staff   s1: 1
tempo   60,4
instr   musique,Clock Tower
*
calc    start ^= 4
calc    m ^= 0
*
macro   wait(A)
*
* A is the number of notes played in the chime. 0 @< A @< 120.
* First rest to fill out 1 minute, then rest for
* 14 minutes.  (A is number of eighth-notes).
*
* If x notes, (120-x)/2 seconds to wait.  If x' seconds to
* wait, (r,x'/4).  @D (120-(A))/8 is length tag of rest.
*
calc    m ^= m + 1
s1:     (r,(120-(A))/8)
*
* The first minute has been padded out.  Do 14 more
* minutes of rest.
*
measure (m)
*
* Wait here for 14 minutes (each repeat to s1: waits for
* 4 seconds)  2 sequential repeat commands, one with
* 21 times through and one with 10 times through causes
* s1: to be executed 210 times; 210 ^* 4 seconds ^/ 60
* = 14 minutes.
*
s1:     (r,1)
repeat  (m),(m),(m),(m),(m),(m),(m),(m),(m),(m),(m),(m),(m),(m),(m),(m),(m),(m),(m),(m),x
repeat  (m),(m),(m),(m),(m),(m),(m),(m),(m),x
endmacro
*
macro   chimes
*
s1:     5e8 d c 4g r r g 5d e c r r e d c 4g r r
        5c d e c r r
*
loop    ring ^= 1,hour
.       s1:     3c4 r
endloop
*
* Each ring lasts for 4 eighth-notes.
*
wait(24 + 4*hour)
s1:     5e8 d c 4g
wait(4)
s1:     5e8 d c 4g r r g 5d e c
wait(10)
s1:     5e8 d c 4g r r g 5d e c r r e d c 4g
wait(16)
*
endmacro
*
* Here begins the fun!
*
*-- measure begin
calc    hour ^= start + 1
*-- hour
loop 
measure hour
*
chimes
*
calcs   hour = 12,hour ^= 1,hour + 1
*-- goto    (hour @= start),hour,x
endloop hour = start
*-- repeat  begin
